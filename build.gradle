buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

plugins {
    id "net.ltgt.apt" version "0.10"
    id "net.researchgate.release" version "2.6.0"
}

apply plugin: "java"
apply plugin: "maven"
apply plugin: 'signing'

group "io.zhudy.duic"

sourceCompatibility = 1.8
targetCompatibility = 1.8
[compileJava, compileTestJava, javadoc]*.options*.encoding = "UTF-8"

repositories {
    mavenLocal()
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
    mavenCentral()
}

ext {
    springVersion = "5.0.5.RELEASE"
    springBootVersion = "2.0.1.RELEASE"
    springCloudVersion = "2.0.0.RC1"
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.16.18"
    compileOnly "org.springframework.boot:spring-boot-autoconfigure:$springBootVersion"

    compile "org.slf4j:slf4j-api:1.7.25"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.3"
    compile "org.springframework:spring-web:$springVersion"
    compile "org.springframework.cloud:spring-cloud-context:$springCloudVersion"

    testCompile "com.squareup.okhttp3:mockwebserver:3.10.0"
    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"

    apt "org.projectlombok:lombok:1.16.18"
    apt "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty("signing.keyId")) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: System.env.OSSRH_USERNAME, password: System.env.OSSRH_PASSWORD)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: System.env.OSSRH_USERNAME, password: System.env.OSSRH_PASSWORD)
                }

                pom.project {
                    name "duic-spring-cloud-config-client"
                    artifactId "duic-spring-cloud-config-client"
                    packaging "jar"
                    description "DuiC 统一配置管理 Spring Cloud Client 工具包"
                    url "https://github.com/zhudyos/duic"

                    scm {
                        url "https://github.com/zhudyos/duic.git"
                        connection "scm:git:git://github.com/zhudyos/duic.git"
                        developerConnection "scm:git:git@github.com:zhudyos/duic.git"
                    }

                    licenses {
                        license {
                            name "The Apache License, Version 2.0"
                            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id "kevin70"
                            name "Kevin Zou"
                            email "kevinz@weghst.com"
                        }
                    }
                }
            }
        }
    }
}
